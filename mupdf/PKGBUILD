# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Pierre-Paul Paquin <pierrepaulpaquin@gmail.com>
# Contributor: xduugu

pkgbase=mupdf
pkgname=(libmupdf mupdf mupdf-gl mupdf-tools)
pkgver=1.24.2
pkgrel=1
pkgdesc='Lightweight PDF and XPS viewer'
arch=('x86_64')
url='https://mupdf.com/'
license=('AGPL3')
makedepends=('desktop-file-utils' 'freetype2' 'freeglut' 'glu' 'harfbuzz' 'zlib'
             'jbig2dec' 'libjpeg' 'mesa-libgl' 'openjpeg2' 'libxext'
             'gumbo-parser')
# we need static libs for zathura-pdf-mupdf
options=('staticlibs')
source=("https://mupdf.com/downloads/archive/mupdf-${pkgver/_/}-source.tar.gz"
        'mupdf.desktop'
        'mupdf.xpm'
	'my.patch')
sha256sums=('7fbced6d6024608661d773b93990119db7eb2483f0d3e28855eadef9ca9a8686'
            'ccff66979249bd4ab4ba8918660f194eb90eb0ae231b16e36a6cecdcf471883f'
            'a435f44425f5432c074dee745d8fbaeb879038ec1f1ec64f037c74662f09aca8'
            '8d9d78bf627703a8c616012a577602920326399ea41b409a0b3d18a40b286969')

prepare() {
  cd $pkgbase-${pkgver/_/}-source

  # remove bundled packages, we want our system libraries
  rm -rf thirdparty/{freeglut,freetype,harfbuzz,jbig2dec,libjpeg,openjpeg,zlib}

  # format gl-main to apply patch
  patch platform/gl/gl-main.c ../my.patch
}

build() {
  CFLAGS+=' -fPIC'
  CXXFLAGS+=' -fPIC'
  export CFLAGS CXXFLAGS
  
  USE_SYSTEM_LIBS='yes'
  export USE_SYSTEM_LIBS

  export CFLAGS+=' -ffat-lto-objects'

  cd $pkgbase-${pkgver/_/}-source
  make shared=yes build=release libs apps
}

package_libmupdf() {
  pkgdesc='Library for Lightweight PDF and XPS viewer'

  cd $pkgbase-${pkgver/_/}-source

  make shared=yes build=release prefix="$pkgdir"/usr install

  rm -rf "$pkgdir"/usr/{bin,share/man}
  mv "$pkgdir"/usr/share/doc/mupdf "$pkgdir"/usr/share/doc/libmupdf

  find "$pkgdir"/usr/include "$pkgdir"/usr/share "$pkgdir"/usr/lib \
    -type f -exec chmod 0644 {} +
}

package_mupdf() {
  pkgdesc='Lightweight PDF and XPS viewer'
  depends=('desktop-file-utils' 'libmupdf' 'libxext')

  cd $pkgbase-${pkgver/_/}-source

  install -D -m0755 build/shared-release/mupdf-x11 "$pkgdir"/usr/bin/mupdf

  install -D -m0644 docs/man/mupdf.1 "$pkgdir"/usr/share/man/man1/mupdf.1

  install -d "$pkgdir"/usr/share/doc/mupdf
  install -m0644  README COPYING CHANGES "$pkgdir"/usr/share/doc/mupdf

  install -D -m0644 ../mupdf.desktop "$pkgdir"/usr/share/applications/mupdf.desktop
  install -D -m0644 ../mupdf.xpm "$pkgdir"/usr/share/pixmaps/mupdf.xpm
}

package_mupdf-gl() {
  pkgdesc='Lightweight PDF and XPS viewer with OpenGL backend'
  conflicts=('mupdf')
  provides=("mupdf=${pkgver}")
  depends=('desktop-file-utils' 'glut' 'libgl' 'libmupdf' 'openssl')

  cd $pkgbase-${pkgver/_/}-source

  install -D -m0755 build/shared-release/mupdf-gl "$pkgdir"/usr/bin/mupdf

  install -D -m0644 docs/man/mupdf.1 "$pkgdir"/usr/share/man/man1/mupdf.1

  install -d "$pkgdir"/usr/share/doc/mupdf
  install -m0644 README COPYING CHANGES "$pkgdir"/usr/share/doc/mupdf

  install -D -m0644 ../mupdf.desktop "$pkgdir"/usr/share/applications/mupdf.desktop
  install -D -m0644 ../mupdf.xpm "$pkgdir"/usr/share/pixmaps/mupdf.xpm
}

package_mupdf-tools() {
  pkgdesc='Tools for Lightweight PDF and XPS viewer'
  depends=('libmupdf')

  cd $pkgbase-${pkgver/_/}-source

  install -D -m0755 build/shared-release/mutool "$pkgdir"/usr/bin/mutool
  install -D -m0755 build/shared-release/muraster "$pkgdir"/usr/bin/muraster

  install -D -m0644 docs/man/mutool.1 "$pkgdir"/usr/share/man/man1/mutool.1

  install -d "$pkgdir"/usr/share/doc/mupdf-tools
  install -m0644 README COPYING CHANGES "$pkgdir"/usr/share/doc/mupdf-tools
}

